apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.yinhaide'

// 导入统一配置项
def info = rootProject.ext.android
def library = rootProject.ext.dependencies

android {
    compileSdkVersion info.compileSdkVersion
    defaultConfig {
        minSdkVersion info.minSdkVersion
        targetSdkVersion info.targetSdkVersion
        versionCode info.versionCode
        versionName info.versionName
        multiDexEnabled info.multiDexEnabled
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // 代码风格支持Java1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 统一限定资源的前缀,防止资源同名导致不可控异常
    resourcePrefix "${project.name}_"

    // 动态修改aar的名字
    android.libraryVariants.all { variant ->
        if(variant.name.equalsIgnoreCase("release")) {
            variant.outputs.all { output ->
                def f = output.outputFileName
                if (f != null && f.endsWith('.aar')) {
                    def fileName = "rocket-v${defaultConfig.versionName}.aar"
                    output.outputFileName = fileName
                }
            }
        }
    }
}

dependencies {
    // 统一基础包
    api library.v4
    api library.v7
    api library.multidex
    // 第三方包
}
